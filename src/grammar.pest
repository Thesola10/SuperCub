alpha = _{'a'..'z'|'A'..'Z'}
digit = _{'0'..'9'}

WHITESPACE   = _{" " | "\t" | NEWLINE}

_sl_comment = @{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
_ml_comment = @{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
COMMENT = _{ _sl_comment | _ml_comment }

ident = @{ (alpha | "_") ~ (alpha|digit|"_")* }

type_ident = { "ident" }
type_expr = { "expr" }
type_tt = { "tt" }
type = _{ type_ident | type_expr | type_tt }

param = { "$" ~ ident ~ (":" ~ type)? }
param_set = { "(" ~ (param ~ ("," ~ param)* )? ~ ")" }

var_base = @{ "$" ~ ident }
var_num = @{ "$" ~ ('0'..'9')}
var_star = @{ "$*" }
var_any = { var_star | var_num | var_base }
var_string = @{ "#" ~ var_any }
assign_eq = { "=" }
assign_append = { "+=" }
assign = _{ assign_eq | assign_append }
var_op = { "${" ~ ident ~ assign ~ c_inner_chunk ~ c_stmt_inner ~ "}" }

macro_call = { ident ~ "!" ~ arg_set }
infix = { macro_call | var_op | var_string | var_any }

arg_inner = _{ !("("|","|")") ~ ANY }
argument = { arg_inner+ }
arg_set = { "(" ~ (argument ~ ("," ~ argument)* )? ~ ")" }

match = { param_set ~ "=>" ~ "{" ~ c_chunk* ~ "}" }
fn_match = { "fn" ~ match }
var_match = { "var" ~ match }
type_match = { "type" ~ match }

any_match = _{ fn_match | var_match | type_match | match }


kw_macro_rules = _{"macro_rules!"}
macro_rules = { kw_macro_rules ~ ident ~ "{" ~ any_match* ~ "}" }

c_block_inner = _{ !("{"|"}") ~ ANY }
c_bare_block = _{ "{" ~ (infix|c_block_inner)* ~ c_bare_block* ~ (infix|c_block_inner)* ~ "}" }
c_block = { (infix|c_block_inner)* ~ c_bare_block }

c_stmt_inner = _{ !("{"|";"|"}") ~ ANY }
c_stmt = { (infix|c_bare_block|c_stmt_inner)* ~ ";" }

c_incl_inner = _{ !("\""|"<"|">") ~ ANY }
c_incl_sys = @{ "<" ~ c_incl_inner* ~ ">" }
c_incl_str = @{ "\"" ~ c_incl_inner* ~ "\"" }
c_incl_path = { c_incl_sys | c_incl_str }
c_include = { "#" ~ "include" ~ c_incl_path }


c_macro = @{ "#" ~ (!NEWLINE ~ ANY)* ~ !"\\" ~ NEWLINE }
c_chunk = { c_include | c_macro | infix | c_stmt | c_block }
c_inner_chunk = { infix | c_block | c_stmt_inner* }

decor = { "#[" ~ ident ~ arg_set? ~ "]" ~ c_chunk }

chunk = _{ COMMENT | NEWLINE | macro_rules | decor | c_chunk }
document = { (chunk)* }
